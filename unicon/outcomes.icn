procedure partitions(cards, subtotal)
    local m, total
    m := 0
    # Hit
    every i:=1 to 10 do {
        if (cards[i]>0) then {
            total := subtotal+i
            if (total < 21) then {
                # Stand
                m +:= 1
                # Hit again
                cards[i] -:= 1
                m +:= partitions(cards, total)
                cards[i] +:= 1
                }
            else if (total==21) then {
                # Stand; hit again is an automatic bust
                m +:= 1
                break
            }
        }
    }
    return m
end

procedure main(argv)
    
    output := &output
    
    deck := [4,4,4,4,4,4,4,4,4,16]
    d := 0

    every i:=1 to 10 do {
        # Dealer showing
        deck[i] -:= 1
        p := 0
        every j:=1 to 10 do {
            deck[j] -:= 1
            p +:= partitions(deck, j)
            deck[j] +:= 1
        }
        write(output,"Dealer showing ", i-1," partitions = ",p)
        d +:= p
	deck[i] +:= 1
    }
    write(output,"Total partitions = ",d)
end
