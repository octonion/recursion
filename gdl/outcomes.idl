function partitions,cards,subtotal
  m = 0L
  ; Hit
  for i=0,9 do begin
      if (cards[i] gt 0) then begin
        total = subtotal+i+1
        if (total lt 21) then begin
          cards[i] = cards[i]-1
          m += 1                       ; Stand
          m += partitions(cards,total) ; Hit again
	  cards[i] = cards[i]+1
	endif else if (total eq 21) then m += 1 ; Can only stand
      endif
  endfor
  return,m
end

deck = [4,4,4,4,4,4,4,4,4,16]

d = 0

for i=0,9 do begin
    ; Dealer showing
    deck[i] = deck[i]-1
    p = 0
    for j=0,9 do begin
        deck[j] = deck[j]-1
        n = partitions(deck,j+1)
        deck[j] = deck[j]+1
        p += n
    endfor
    print,'Dealer showing ',i,' partitions =',p
    d += p
    deck[i] = deck[i]+1
endfor

print,'Total partitions =',d

end
