def partitions(cards, subtotal)
{ def result := 0;

  // Hit
  10.doTimes: { |i|
    if: (cards[i]>0) then: {
      def total := subtotal+i;
      if: (total < 21) then: {
        // Stand
        result := result+1;
        // Hit again
        cards[i] := cards[i]-1;
        result := result+partitions(cards, total);
        cards[i] := cards[i]+1;
      }
      else: { if: (total==21) then: {
                // Stand; hit again is an automatic bust
		result := result+1;
	      }
      };
    };
  };
  result;
};

def deck := [4, 4, 4, 4, 4, 4, 4, 4, 4, 16];
def d := 0;


10.doTimes: { |i|
    // Dealer showing
    deck[i] := deck[i]-1;
    def p := 0;
    10.doTimes: { |j|
        deck[j] := deck[j]-1;
        p := p+partitions(deck, j);
        deck[j] := deck[j]+1;
    };
    system.println("Dealer showing ",i-1," partitions = ",p);
    d := d+p;
    deck[i] := deck[i]+1;
};

system.println("Total partitions = ",d);
system.exit();


